// WARNING: You must use Java 8 with Gradle 5 (DO NOT USE Java 9+ or Gradle 6+)

plugins {
    id 'war'
    id "com.eriwen.gradle.css" version "2.14.0"
    id "eu.butter.gradle.js" version "2.15.1"
    // id "org.padler.gradle.minify" version "1.7.0" // TODO: Replace above plugins with this one as above don't work with Gradle 6
}

description = "EPICS to web gateway"
group 'org.jlab'
version = '1.11.0'

ext {
    releaseDate = 'December 14 2020'
    productionRelease = 'true'
}

repositories {
    mavenCentral()
}

/*compileJava { // This is only needed to cross-compile using Java 11 targeting Java 8; invalid if you use Java 8 to begin with!
    options.compilerArgs.addAll(['--release', '8'])
}*/

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    compile 'javax.servlet:jstl:1.2'
    implementation 'org.glassfish:javax.json:1.1.4'
    implementation files('lib/jca-2.4.6.jar')
    //implementation 'org.epics:jca:2.4.6' // Only works with Java 11 runtime
    providedCompile 'javax:javaee-api:7.0'

    testImplementation 'junit:junit:4.13'

    testCompile "org.testcontainers:testcontainers:1.14.3"
    testCompile 'org.slf4j:slf4j-simple:1.6.1'
}

test {
    classpath = project.sourceSets.test.runtimeClasspath + files("${projectDir}/examples/softioc-db")

    testLogging {
        showStandardStreams = true
    }
}

war {
    dependsOn(minifyCss)
    dependsOn(minifyJs)
    archiveName 'epics2web.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
        filter {
            String line -> line.replaceAll("@PRODUCTION_RELEASE@", productionRelease)
        }
    }
    from('build') {
        include '*.min.css'
        into 'resources/css/'
    }
    from('build') {
        include '*.min.js'
        into 'resources/js/'
    }
}

minifyCss {
    source = "${projectDir}/src/main/webapp/resources/css/epics2web.css"
    dest = file("${projectDir}/build/epics2web.min.css")
}

minifyJs {
    source = "${projectDir}/src/main/webapp/resources/js/epics2web.js"
    dest = file("${projectDir}/build/epics2web.min.js")
}