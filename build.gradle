plugins {
    id 'war'
    id "com.eriwen.gradle.css" version "2.14.0"
    id "eu.butter.gradle.js" version "2.15.1"
}

description = "EPICS to web gateway"
group 'org.jlab'
version = '1.9.3'

def junitJupiterVersion = '5.4.2'

ext {
    releaseDate = 'March 11 2020'
    productionRelease = 'true'
}

repositories {
    mavenCentral()
}

compileJava   {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

test {
    useJUnitPlatform()
}


dependencies {
    compile 'javax.servlet:jstl:1.2'
    implementation 'org.glassfish:javax.json:1.1.4'
    implementation 'org.epics:jca:2.3.6'
    implementation 'org.epics:caj:1.1.15'
    providedCompile 'javax:javaee-api:7.0'

    testImplementation 'junit:junit:4.13'

    testImplementation "org.testcontainers:testcontainers:1.14.3"
}

war {
    dependsOn(minifyCss)
    dependsOn(minifyJs)
    archiveName 'epics2web.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
        filter {
            String line -> line.replaceAll("@PRODUCTION_RELEASE@", productionRelease)
        }
    }
    from('build') {
        include '*.min.css'
        into 'resources/css/'
    }
    from('build') {
        include '*.min.js'
        into 'resources/js/'
    }
}

minifyCss {
    source = "${projectDir}/src/main/webapp/resources/css/epics2web.css"
    dest = file("${projectDir}/build/epics2web.min.css")
}

minifyJs {
    source = "${projectDir}/src/main/webapp/resources/js/epics2web.js"
    dest = file("${projectDir}/build/epics2web.min.js")
}